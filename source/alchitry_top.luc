module alchitry_top (
    input clk,              // 10MHz clock
    // input rst_n,            // reset button (active low)
    output led[8],          // 8 user controllable LEDs
    output io_led[8],       // 8 debugging leds
    input usb_rx,           // USB->Serial input
    output usb_tx,          // USB->Serial output

    // buttons
    input butt_dirs[4],     // direction buttons
    input butt_sel_desel[2],// select and deselect buttons
    input butt_reset,       // reset button, to override reset button
    input butt_next_play,   // next and play button, action based on FSM state
    // ^ to pass through edge detector
    
    // matrix connections
    output mataddr[5],      // ABCDE addresses
    output mattop[3],       // top led data
    output matbot[3],       // bot led data
    output matclk,          // clock
    output matoe,           // OE
    output matlat,          // latch
    output matgnd[2],       // ground

    // seven segment leds
    output aseg[12],      // counter for player A
    output bseg[12],      // counter for player B
    output timerseg[12]   // game timer

    // testing
    // input testpullup        // default high?

) {
    const CLK = 10_000_000

    // arithmetic logic unit
    alu alum(#SIZE(13))
    
    sig rst
    sig fifo_rst
    sig slowclk

    clk_10 slowclk_gen
    always {
        slowclk_gen.reset = 0
        slowclk_gen.clk_in1 = clk
        slowclk = slowclk_gen.clk_out1
    }
    
    .clk(clk), .rst(rst) {
            // seven segment drivers
            multi_seven_seg aseg_driver
            multi_seven_seg bseg_driver
            multi_seven_seg timerseg_driver
    }

    .clk(slowclk) {
    
        reset_conditioner reset_cond        // whole board reset

        // button conditioners
        // button_conditioner cond_butt_dirs[4](#CLK_FREQ(4x{{CLK}}),#MIN_DELAY(4x{{200}}),#NUM_SYNC(4x{{30}}))
        // button_conditioner cond_butt_sel_desel[2](#CLK_FREQ(2x{{CLK}}),#MIN_DELAY(2x{{200}}),#NUM_SYNC(2x{{30}}))
        // button_conditioner cond_butt_next_play(#CLK_FREQ(CLK),#MIN_DELAY(200),#NUM_SYNC(30))
        button_conditioner cond_butt_dirs[4](#CLK_FREQ(4x{{CLK}}))
        button_conditioner cond_butt_sel_desel[2](#CLK_FREQ(2x{{CLK}}))
        button_conditioner cond_butt_next_play(#CLK_FREQ(CLK))

        // edge detector for next/play button
        edge_detector edge_next_play(#RISE(1),#FALL(0))
        
        // edge detecors for internal clocks
        edge_detector timerclk(#RISE(1),#FALL(0))       // ticks once per second
        edge_detector gameclk(#RISE(1),#FALL(0))        // ticks roughly 10 times per second
        edge_detector med_inputclk(#RISE(1),#FALL(0))   // ticks roughly 5 times per second
        
        // dual bram module
        bram brams
            
        .rst(rst) {
        
            // display driver
            display_driver display(#CLK_FREQ(CLK))
            
            // counters
            counter timercounter(#SIZE($clog2(CLK)),#TOP(CLK))                  // 1 tick per second
            counter gamecounter(#SIZE($clog2(CLK/4224/6)),#TOP(CLK/4224/6))     // 4 generations per second
            
            // FSM
            fsm sm
            
            // custom 13-bit regfile
            regfile reg(#WIDTH(13))
        }

        .rst(fifo_rst) {
            // shift registers
            shift_register sr1(#WIDTH(2),#ENTRIES(4))
            shift_register sr2(#WIDTH(2),#ENTRIES(4))
            shift_register sr3(#WIDTH(2),#ENTRIES(4))
        }
    }


    always {
        
        io_led[3:0] = cond_butt_dirs.out[3:0]
        io_led[5:4] = cond_butt_sel_desel.out[1:0]
        io_led[6] = cond_butt_next_play.out
        io_led[7] = reset_cond.out
        
        // whole board reset
        reset_cond.in = ~butt_reset
        rst = reset_cond.out

        // default board led and usb connection
        led = 8h00
        usb_tx = usb_rx
        
        // button processing
        cond_butt_dirs.in[3:0] = ~butt_dirs[3:0]
        cond_butt_sel_desel.in[1:0] = ~butt_sel_desel[1:0]
        cond_butt_next_play.in = ~butt_next_play
        edge_next_play.in = cond_butt_next_play.out

        // matrix and display driver connections
        mataddr = display.address
        mattop = display.toppixel
        matbot = display.botpixel
        matclk = display.sclk_out
        matoe = display.blank
        matlat = display.latch
        matgnd = 0

        sm.ddr = display.reading
        
        // connect slow clocks
        timerclk.in = timercounter.value[$clog2(CLK)-1]
        gameclk.in = gamecounter.value[$width(gamecounter.value)-1]
        med_inputclk.in = timercounter.value[$clog2(CLK)-7]
        sm.timerclk = timerclk.out
        sm.gameclk = gameclk.out
        sm.med_inputclk = med_inputclk.out

        // buttons connections
        sm.next_start_button = edge_next_play.out
        sm.move_up_button = cond_butt_dirs.out[0]
        sm.move_down_button = cond_butt_dirs.out[1]
        sm.move_left_button = cond_butt_dirs.out[2]
        sm.move_right_button = cond_butt_dirs.out[3]
        sm.select_button = cond_butt_sel_desel.out[0]
        sm.deselect_button = cond_butt_sel_desel.out[1]

        // seven segment // ~ if not cathode/anode
        aseg_driver.value = reg.pac
        bseg_driver.value = reg.pbc
        timerseg_driver.value = reg.timer
        aseg = ~aseg_driver.out
        bseg = ~bseg_driver.out
        timerseg = ~timerseg_driver.out
        
        // FSM and regfile connections
        reg.ra1 = sm.ra1
        reg.ra2 = sm.ra2
        reg.wa = sm.wa
        reg.we = sm.we
        sm.rd1 = reg.rd1
        sm.rd2 = reg.rd2
        sm.pac = reg.pac
        sm.pbc = reg.pbc
        sm.timer = reg.timer
        
        // alu
        sm.aluout = alum.out
        // sm.aluout_lsb = alum.out[0]
        alum.alufn = sm.alufn
        
        // a selector
        case(sm.asel) {
            0: alum.a = reg.rd1
            default: alum.a = reg.rd1
        }
        
        // b selector
        case(sm.bsel) {
            0: alum.b = reg.rd2
            1: alum.b = 1
            2: alum.b = 63
            3: alum.b = 64
            4: alum.b = 65
            default: alum.b = reg.rd2
        }

        // wd selector
        case(sm.wdsel) {
            0: reg.wd = alum.out
            1: reg.wd = 0
            2: reg.wd = 1
            3: reg.wd = 2
            4: reg.wd = brams.brd
            5: reg.wd = sr2.dout
            6: reg.wd = 60
            7: reg.wd = 2144
            8: reg.wd = 64
            default: reg.wd = alum.out
        }

        // shift register connections
        // reset conditioner
        // reset
        fifo_rst = sm.srst | reset_cond.out
        // write enable
        sr1.wput = sm.swe1
        sr2.wput = sm.swe2
        sr3.wput = sm.swe3
        // write data
        sr1.din = brams.brd
        sr2.din = brams.brd
        sr3.din = brams.brd
        // read enable
        sr1.rget = sm.sre1
        sr2.rget = sm.sre2
        sr3.rget = sm.sre3
        // read data
        sm.srd1 = sr1.dout
        sm.srd2 = sr2.dout
        sm.srd3 = sr3.dout

        // BRAM and display driver connections
        display.data = brams.brd
        brams.bra_override = display.addr
        brams.ro = display.reading
        
        // BRAM and FSM connections
        brams.brsel = sm.brsel
        brams.bra = alum.out
        brams.bwa = reg.rd1
        brams.bwe = sm.bwe
        brams.bwd = sm.bwd
        
        // BRAM and regfile connections
        brams.bram_selector = reg.bram_selector
    }
}